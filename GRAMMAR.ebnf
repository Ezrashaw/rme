/* common types */
<digit>      ::= [0-9]
<number>     ::= (<digit> | ".")+
<letter>     ::= [a-z] | [A-Z]
<identifier> ::= <letter>+

<literal>    ::= <number> | "true" | "false"

/* the root program node */
<program>          ::= (<statement> ";")+
/* helpful REPL rule, allows semicolons to be omitted */
<single_statement> ::= <statement> ";"?

<statement> ::= <return> | <var_def> | <fn_def> | <expression>

/* statement definitions */
<return> ::= "return " <expression>
<var_def> ::= "let " <identifier> "=" <expression>
<fn_def>  ::= "fn " <identifier> "(" <fn_def_arg_list> ")" "=" <expression>

/* expression tree. further down = higher precedence */
<expression>          ::= <equality_expression>
<equality_expression> ::= <additive_expression> ("==" <additive_expression>)*
<additive_expression> ::= <term> (("+" | "-") <term>)*
<term>                ::= <unary_prefix> (("*" | "/") <unary_prefix>)*
<unary_prefix>        ::= "-" <unary_prefix> | <unary_postfix>
<unary_postfix>       ::= <fn_call> "!"*
<fn_call>             ::= <factor> ("(" <fn_call_arg_list> ")")*
<factor>              ::= <literal> | "(" <expression> ")" | <identifier>

/* complex grammar constructs */
<fn_call_arg_list>    ::= (<expression> ("," <expression>)*)?
<fn_def_arg_list>     ::= (<identifier> ("," <identifier>)*)?